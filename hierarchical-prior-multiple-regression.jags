model {
  ## likelihood
  ##
  for (i in 1:n.samp) {
    lma[i] ~ dnorm(mu[i], tau.resid)
    mu[i] <- beta.0[species[i]] + beta.mat*mat[i] + beta.map*map[i] 
             + beta.ratio*ratio[i] + beta.cdd*cdd[i]
             + beta.inso*inso[i] + beta.elev*elev[i]
  }

  ## priors
  ##
  ## regression coefficients
  ##
  for (i in 1:n.species) {
    beta.0[i] ~ dnorm(beta.zero, tau.species)
  }
  ##
  ## t distribution with 1 df is Cauchy
  ##
  beta.zero ~ dt(0.0, tau, 1.0)
  beta.mat ~ dt(0.0, tau, 1.0)
  beta.map ~ dt(0.0, tau, 1.0)
  beta.ratio ~ dt(0.0, tau, 1.0)
  beta.cdd ~ dt(0.0, tau, 1.0)
  beta.inso ~ dt(0.0, tau, 1.0)
  beta.elev ~ dt(0.0, tau, 1.0)
  ## 
  ## residual variance
  ##
  tau.resid ~ dgamma(1.0, phi)
  sigma.resid <- sqrt(1.0/tau.resid)
  ##
  ## species random effect variance
  ##
  tau.species ~ dgamma(1.0, phi)
  sigma.species <- sqrt(1.0/tau.species)
  ##
  ## variance of Cauchy
  ##
  tau ~ dgamma(nu.0/2.0, nu.0*sigma2.0/2)
}